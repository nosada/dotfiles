#!/bin/fish

set script_name (basename (status -f))

function usage
	string trim "
Initialize / update your daily-use environment

Usage:
	$script_name [ssh-key|fisherman|venv]

Options:
	ssh-key		Load your SSH private keys to ssh-agent
	fisherman	Update fish shell plugins installed by fisherman
	venv-pkgs	Update python packages installed on your venv(s)
	<none>		Do all the above
"
end

function load_ssh_keys
	set BASEDIR "$HOME/.ssh"
	for SUBDIR in (ls -l $BASEDIR | grep '^d' | awk '{print $8}' | sed -e 's/\///g')
		set SSH_DIR "$BASEDIR/$SUBDIR"
		report "Load SSH keys in $SSH_DIR"
		for KEY in (ls $SSH_DIR | grep id_ | grep -v '.pub')
			set SSH_KEY "$SSH_DIR/$KEY"
			get --prompt "Load '$SSH_KEY' ? [y/N]" | read -l CONFIRM
			switch $CONFIRM
				case Y y
					ssh-add "$SSH_KEY"
			end
		end
	end
end

function update_fisherman_plugins
	report "Update fisherman plugins"
	fisher update
end

function update_pkgs_in_venvs
	set BASEDIR "$HOME/Venvs"
	for SUBDIR in (ls -l $BASEDIR | grep '^d' | awk '{print $8}' | sed -e 's/\///g')
		set VENV_DIR "$BASEDIR/$SUBDIR"
		set ACTIVATE "source $VENV_DIR/bin/activate"
		report "Update packages in venv $VENV_DIR"
		set PIP_PKGS (bash -c "$ACTIVATE; pip list --format=freeze | cut -d '=' -f 1")
		bash -c "$ACTIVATE; pip install --upgrade $PIP_PKGS"

		# TODO: remove below downgrading of prompt-toolkit
		# after ipython supports prompt-toolkit >= 2
		# (cf.https://github.com/jupyter/jupyter_console/issues/158)
		if test (echo $PIP_PKGS | grep ipython)
			bash -c "$ACTIVATE; pip install 'prompt-toolkit==1.0.15'"
		end
	end
end

function report
	echo -e "\033[1;37m$argv\033[0;39m"
end

function hello
	argparse -n $script_name 'h/help' -- $argv
	or return 1

	if set -lq _flag_h
		usage > /dev/stderr
		return 1
	end

	report "Hello!"
	if test -z "$argv"
		load_ssh_keys
		update_fisherman_plugins
		update_pkgs_in_venvs
	else
		switch "$argv"
		case "ssh-key"
			load_ssh_keys
		case "fisherman"
			update_fisherman_plugins
		case "venv-pkgs"
			update_pkgs_in_venvs
		end
	end
	report "Done! Have a nice day!"
end

hello $argv
